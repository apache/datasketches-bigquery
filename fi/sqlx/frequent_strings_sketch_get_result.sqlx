/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

config { hasOutput: true, tags: ["fi", "udfs"] }

CREATE OR REPLACE FUNCTION ${self()}(sketch BYTES, error_type STRING, threshold INT64)
RETURNS ARRAY<STRUCT<item STRING, estimate INT64, lower_bound INT64, upper_bound INT64>>
LANGUAGE js
OPTIONS (
  library=["${dataform.projectConfig.vars.jsBucket}/fs_sketch.js"],
  js_parameter_encoding_mode='STANDARD',
  description = '''Returns an array of rows that include frequent items, estimates, lower and upper bounds
given an error_type and a threshold.

Param sketch: the given sketch as sketch encoded bytes.
Param error_type: determines whether no false positives or no false negatives are desired.
Param threshold: a threshold to include items in the result list.
If NULL, the maximum error of the sketch is used as a threshold.
Returns: an array of frequent items with frequency estimates, lower and upper bounds.

For more information:
 - https://datasketches.apache.org/docs/Frequency/FrequencySketches.html
'''
) AS R"""
if (sketch == null) return null;
try {
  return Module.frequent_strings_sketch.getResult(sketch, error_type, threshold ? threshold : 0);
} catch (e) {
  if (e.message != null) throw e;
  throw new Error(Module.getExceptionMessage(e));
}
""";
