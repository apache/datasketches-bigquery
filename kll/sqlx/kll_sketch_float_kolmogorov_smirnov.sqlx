/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

config { hasOutput: true, tags: ["kll", "udfs"] }

CREATE OR REPLACE FUNCTION ${self()}(sketchA BYTES, sketchB BYTES, pvalue FLOAT64)
RETURNS BOOL
LANGUAGE js
OPTIONS (
  library=["${dataform.projectConfig.vars.jsBucket}/kll_sketch_float.js"],
  js_parameter_encoding_mode='STANDARD',
  description = '''Performs the Kolmogorov-Smirnov Test between two KLL sketches of type FLOAT64.
If the given sketches have insufficient data or if the sketch sizes are too small, this will return false.

Param sketchA: sketch A in serialized form.
Param sketchB: sketch B in serialized form.
Param pvalue: Target p-value. Typically 0.001 to 0.1, e.g. 0.05.
Returns: boolean indicating whether we can reject the null hypothesis (that the sketches
  reflect the same underlying distribution) using the provided p-value.

For more information:
 - https://datasketches.apache.org/docs/KLL/KLLSketch.html
'''
) AS R"""
if (sketchA == null || sketchB == null) return null;
try {
  return Module.kolmogorovSmirnovTest(sketchA, sketchB, pvalue);
} catch (e) {
  if (e.message != null) throw e;
  throw new Error(Module.getExceptionMessage(e));
}
""";
